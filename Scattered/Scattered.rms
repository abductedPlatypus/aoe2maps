/* ****************************** */
/*         ScatterEverything      */
/* ****************************** */
/*   
    A game designer's attempt at a Baobab "Nothing" map, but turned out to be a highly technical map that seems to favour Khmer.
    On this map forests, gold and stone are scattered so placing your camps will take some getting used to. Even Farms will be hard to place. 
    The forest will force you to place buildings quite far from your TC. Luckily there is plenty of food and even some extra elephants that can be lured to you TC with your scout. 
    An additional horse is added for exploring white doing that. Make sure your micro isn't rusty. 

    No history lessons on this one. Though I hope this map might make Khmer a bit more relevant, with their no building requirement, 
    their Castle age ability to destroy trees and ability to garisson villagers in houses. 
    And if they are given more playtime more Khmer strategies might be found.

    Version 1.0, 2018-07-08
    Version 1.1, 2018-07-09 - Moved elephants a bit more to the center
    Version 1.2, 2018-07-10 - Redistributed gold and stone
    Version 1.3, 2018-07-12 - Reduced replace one near lurable with hugging berries, added center eyecandy

    Any comments, changes and suggestions welcomed at 
    https://github.com/abductedPlatypus/aoe2maps
    If you make any changes I'd love it if you let me know!

    @abductypus (Twitter, preferred!)
    abductedPlatypus (AoEZone, Reddit, Twitch)

    This work is licensed under the Creative Commons Attribution 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by/4.0/.
*/

/* ********************************************** */

/* TODO: Pack into ZR, move elephants bit more to mid, maybe add gold/stone around center hill each corner? */

/* COMMENT TO HIDE */
#define EYECANDY
#define BIRDS
/* #define ZR  */
/* #define NO_SCATTER_MINES */
/* #define NERF_SLING       */

/* Sling nerfs */
#const TRIBUTE_INEFFICIENCY 46
#const BANKING 17
#const COINAGE 23
#const COINAGE_IMP_DISABLER 224


if 0_TEAM_GAME
elseif 1_TEAM_GAME
elseif 2_TEAM_GAME
elseif 3_TEAM_GAME
elseif 4_TEAM_GAME
else
  #define HD
endif

if HD
#const YES_FOREST   49
#const NO_FOREST    44
#const EXTRA_RESOURCE_AREA  42
#const CENTER 41
elseif ZR
#const YES_FOREST   16
#const NO_FOREST    3
#const EXTRA_RESOURCE_AREA 9
#const EXTRA_RESOURCE_AREA 14
else
#const YES_FOREST   16
#const NO_FOREST    3
#const EXTRA_RESOURCE_AREA   14
#const CENTER   9
endif

#const LURABLE_POOL     936
#const ELEPHANT 936
#const POOL_AREA        4

#const LURABLE_NEAR     1139
#const HUNTABLE         1019
#const CATTLE           1060
#const EXTRA_SCOUT      814
#const PREDATOR         1029
#const PREDATOR_POOL    1031
#const BERRIES          1059

#const BIRD             917

#const BOULDER_A        1048
#const BOULDER_B        1049
#const BOULDER_C        1050
#const BEIGE_GRASS      1033
#const GRASS_PATCH      301



<PLAYER_SETUP>
random_placement

if NERF_SLING
  effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 40
  effect_amount DISABLE_TECH BANKING ATTR_DISABLE 17
  effect_amount DISABLE_TECH COINAGE ATTR_DISABLE 23
  effect_amount DISABLE_TECH COINAGE_IMP_DISABLER ATTR_DISABLE 224
endif

<LAND_GENERATION>

base_terrain                        YES_FOREST

create_player_lands 
{ 
  terrain_type                      NO_FOREST
  number_of_tiles                   30
  base_size                         2
  left_border                       10
  top_border                        10
  bottom_border                     10
  right_border                      10
  zone                              1 
}


create_land
{ 
    terrain_type                      CENTER
    land_percent                      100
    left_border                       42
    top_border                        42
    bottom_border                     42
    right_border                      42
    border_fuzziness                  100
    clumping_factor                   100
    zone                              2
    land_id                           2
}

create_land
{ 
    terrain_type                      YES_FOREST
    land_percent                      100
    land_positi                       50 0 
    border_fuzziness                  100
    clumping_factor                   100
    zone                              1
    land_id                           1
}

create_land
{ 
    terrain_type                      POOL_AREA
    number_of_tiles                   6
    base_size                         1
    left_border                       90
    top_border                        90
    bottom_border                     5
    right_border                      5
    zone                              1 
    border_fuzziness                  15
    clumping_factor                   15
    land_id                           50
}
create_land
{ 
    terrain_type                      EXTRA_RESOURCE_AREA
    land_percent                      1
    base_size                         1
    left_border                       75
    top_border                        75
    bottom_border                     0
    right_border                      0
    zone                              1 
    land_position                     100 99
    border_fuzziness                  100
    clumping_factor                   100
}

create_land
{ 
    terrain_type                      POOL_AREA
    number_of_tiles                   6
    base_size                         1
    left_border                       5
    top_border                        5
    bottom_border                     90
    right_border                      90
    zone                              1 
    border_fuzziness                  15
    clumping_factor                   15
    land_id                           51
}
create_land
{ 
    terrain_type                      EXTRA_RESOURCE_AREA
    land_percent                      1
    base_size                         1
    left_border                       0
    top_border                        0
    bottom_border                     75
    right_border                      75
    zone                              1 
    land_position                     0 0
    border_fuzziness                  100
    clumping_factor                   100
}
create_land
{ 
    terrain_type                      POOL_AREA
    number_of_tiles                   6
    base_size                         1
    left_border                       90
    top_border                        5
    bottom_border                     90
    right_border                      5
    zone                              1 
    border_fuzziness                  15
    clumping_factor                   15
    land_id                           52
}
create_land
{ 
    terrain_type                      EXTRA_RESOURCE_AREA
    land_percent                      1
    base_size                         1
    left_border                       75
    top_border                        0
    bottom_border                     75
    right_border                      0
    zone                              1 
    land_position                     99 0
    border_fuzziness                  100
    clumping_factor                   100
} 

create_land
{ 
    terrain_type                      POOL_AREA
    number_of_tiles                   6
    base_size                         1
    left_border                       5
    top_border                        90
    bottom_border                     5
    right_border                      90
    zone                              1 
    border_fuzziness                  15
    clumping_factor                   15
    land_id                           53
}

create_land
{ 
    terrain_type                      EXTRA_RESOURCE_AREA
    land_percent                      1
    base_size                         1
    left_border                       0
    top_border                        75
    bottom_border                     0
    right_border                      75
    zone                              1 
    land_position                     0 99
    border_fuzziness                  100
    clumping_factor                   100
}

<TERRAIN_GENERATION>
create_terrain NO_FOREST
{
  base_terrain                      YES_FOREST
  spacing_to_other_terrain_types    2
  number_of_tiles                   1000
  number_of_clumps                  8
  set_avoid_player_start_areas     
  set_scale_by_size
  set_scale_by_groups
}

create_terrain NO_FOREST
{
  base_terrain                      YES_FOREST
  spacing_to_other_terrain_types    2
  number_of_tiles                   900
  number_of_clumps                  8
  set_avoid_player_start_areas     
  set_scale_by_size
  set_scale_by_groups
}

create_terrain NO_FOREST
{
  base_terrain                      YES_FOREST
  spacing_to_other_terrain_types    2
  number_of_tiles                   1200
  number_of_clumps                  12
  set_avoid_player_start_areas     
  set_scale_by_size
  set_scale_by_groups
} 

<ELEVATION_GENERATION>

create_elevation  4
{
base_terrain CENTER
number_of_tiles 1000
number_of_clumps 1
set_scale_by_size
set_scale_by_groups
spacing 1
}


<CLIFF_GENERATION>
min_number_of_cliffs 50
max_number_of_cliffs 50 


<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius        18
  min_distance_to_players       0
  max_distance_to_players       0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
}

create_object EXTRA_SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
}

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

endif

create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1

  place_on_specific_land_id     50
  group_placement_radius        1
}

create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     51
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     52
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     53
  group_placement_radius        1
}

create_object RELIC
{
  number_of_objects             1
  number_of_groups              4
  terrain_to_place_on           EXTRA_RESOURCE_AREA
  temp_min_distance_group_placement 20
}

create_object RELIC
{
  number_of_objects             1
  number_of_groups              2
  terrain_to_place_on           CENTER
  temp_min_distance_group_placement 12
}

if MEDIUM_MAP
    create_object ELEPHANT
    {
    number_of_objects             1
    number_of_groups              1
    place_on_specific_land_id     50
    group_placement_radius        1
    }
    create_object ELEPHANT
    {
    number_of_objects             1
    number_of_groups              1
    place_on_specific_land_id     51
    group_placement_radius        1
    }
    create_object ELEPHANT
    {
    number_of_objects             1
    number_of_groups              1
    place_on_specific_land_id     52
    group_placement_radius        1
    }
    create_object ELEPHANT
    {
    number_of_objects             1
    number_of_groups              1
    place_on_specific_land_id     53
    group_placement_radius        1
    }
endif

if TINY_MAP
elseif SMALL_MAP
elseif MEDIUM_MAP
else
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     50
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     51
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     52
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     53
  group_placement_radius        1
}
endif


if TINY_MAP
elseif SMALL_MAP
elseif MEDIUM_MAP
elseif LARGE_MAP
else
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     50
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     51
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     52
  group_placement_radius        1
}
create_object ELEPHANT
{
  number_of_objects             1
  number_of_groups              1
  place_on_specific_land_id     53
  group_placement_radius        1
}
endif


create_object GOLD
{
  number_of_objects             4
  number_of_groups              1
  group_placement_radius        3
  if NO_SCATTER_MINES
    set_tight_grouping
  endif
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       12
  max_distance_to_players       16
  min_distance_group_placement  7
  max_distance_to_other_zones   7
}

create_object GOLD
{
  number_of_objects             5
  number_of_groups              1
  group_placement_radius        3
  if NO_SCATTER_MINES
    set_tight_grouping
  endif
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       12
  max_distance_to_players       16
  min_distance_group_placement  7
  max_distance_to_other_zones   7
}

create_object STONE
{
  number_of_objects             4
  number_of_groups              2
  group_placement_radius        3 
  if NO_SCATTER_MINES
    set_tight_grouping
  endif 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       12
  max_distance_to_players       16
  min_distance_group_placement  7
  max_distance_to_other_zones   7
}

create_object CATTLE
{
   number_of_objects                2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players          6
   max_distance_to_players          10
}

create_object CATTLE
{
   number_of_objects                2
   number_of_groups                 3
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players          16
   max_distance_to_players          24
   temp_min_distance_group_placement 10
}

create_object HUNTABLE
{
  number_of_objects                 4
  group_placement_radius            3
  number_of_groups                  1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players           10
  max_distance_to_players           13
  temp_min_distance_group_placement 14
}

create_object LURABLE_NEAR
{
  number_of_objects                 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players           18
  max_distance_to_players           24
}


if BIRDS
create_object BIRD
{
  number_of_objects                 1
  number_of_groups                  4 
  set_gaia_object_only
  set_scaling_to_map_size
  temp_min_distance_group_placement 24
}
endif

if EYECANDY
    if BOULDER_A
        create_object BOULDER_A
        {
          number_of_objects                 1
          number_of_groups                  4
          min_distance_to_players           20
          set_scaling_to_map_size
          terrain_to_place_on               YES_FOREST
          temp_min_distance_group_placement 24
          max_distance_to_other_zones       12
        }
    endif

    if BOULDER_B
        create_object BOULDER_B
        {
          number_of_objects                 1
          number_of_groups                  4
          min_distance_to_players           20
          set_scaling_to_map_size
          terrain_to_place_on               YES_FOREST
          temp_min_distance_group_placement 24
          max_distance_to_other_zones       12
        }
    endif

    if BOULDER_C
        create_object BOULDER_C
        {
          number_of_objects                 1
          number_of_groups                  4
          min_distance_to_players           20
          set_scaling_to_map_size
          terrain_to_place_on               YES_FOREST
          temp_min_distance_group_placement 24
          max_distance_to_other_zones       12
        }
    endif

    if GRASS_PATCH
        create_object GRASS_PATCH
        {
          number_of_objects                 1
          number_of_groups                  4
          terrain_to_place_on               POOL_AREA
        }
    endif

    if GRASS_PATCH
        create_object GRASS_PATCH
        {
          number_of_objects                 1
          number_of_groups                  4
          terrain_to_place_on               POOL_AREA
        }
    endif

    if BEIGE_GRASS
        create_object BEIGE_GRASS
        {
          number_of_objects                 2
          number_of_groups                  2
          temp_min_distance_group_placement 6
          terrain_to_place_on               CENTER
          set_scaling_to_map_size
        }
    endif
endif


create_object BERRIES
{
  number_of_objects             2
  number_of_groups              3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       3
  max_distance_to_players       4
}

/* 
    This goes last so we make sure gold and stone are more apart 
    (non temp)_min_distance_group_placement
*/

create_object ELEPHANT
{
  if TINY_MAP
    number_of_objects             1
    number_of_groups              2
  elseif SMALL_MAP
    number_of_objects             1
    number_of_groups              3
  elseif MEDIUM_MAP
    number_of_objects             1
    number_of_groups              4
  elseif LARGE_MAP
    number_of_objects             2
    number_of_groups              3
  elseif HUGE_MAP
    number_of_objects             2
    number_of_groups              4
  elseif GIGANTIC_MAP
    number_of_objects             4
    number_of_groups              4
  elseif LUDIKRIS_MAP
    number_of_objects             4
    number_of_groups              8
  endif
  
  set_gaia_object_only

  temp_min_distance_group_placement  12
  terrain_to_place_on           CENTER
}

create_object STONE
{
  number_of_objects             5
  number_of_groups              1
  set_scaling_to_map_size 
  if NO_SCATTER_MINES
    set_tight_grouping
  endif
  set_gaia_object_only

  min_distance_group_placement  6
  terrain_to_place_on           CENTER
}


create_object GOLD
{
  number_of_objects             7
  number_of_groups              1
  set_scaling_to_map_size 
  if NO_SCATTER_MINES
    set_tight_grouping
  endif
  set_gaia_object_only

  min_distance_group_placement  6
  terrain_to_place_on           CENTER
}

create_object STONE
{
  if TINY_MAP
    number_of_objects             4
    group_placement_radius        3  
  elseif SMALL_MAP
    number_of_objects             5
    group_placement_radius        4  
  elseif MEDIUM_MAP
    number_of_objects             8
    group_placement_radius        4 
  elseif LARGE_MAP
    number_of_objects             10
    group_placement_radius        5  
  elseif HUGE_MAP
    number_of_objects             12
    group_placement_radius        6  
  elseif GIGANTIC_MAP
    number_of_objects             16
    group_placement_radius        7  
  elseif LUDIKRIS_MAP
    number_of_objects             50
    group_placement_radius        12 
  endif
  number_of_groups                4

  if NO_SCATTER_MINES
    set_tight_grouping
  endif  
  set_gaia_object_only
  temp_min_distance_group_placement  35
  terrain_to_place_on                EXTRA_RESOURCE_AREA
  min_distance_group_placement       6
}


create_object GOLD
{
  if TINY_MAP
    number_of_objects             4
    group_placement_radius        3  
  elseif SMALL_MAP
    number_of_objects             5
    group_placement_radius        4  
  elseif MEDIUM_MAP
    number_of_objects             9
    group_placement_radius        4 
  elseif LARGE_MAP
    number_of_objects             12
    group_placement_radius        5  
  elseif HUGE_MAP
    number_of_objects             14
    group_placement_radius        6  
  elseif GIGANTIC_MAP
    number_of_objects             18
    group_placement_radius        7  
  elseif LUDIKRIS_MAP
    number_of_objects             50
    group_placement_radius        12 
  endif
  number_of_groups              4
  
  if NO_SCATTER_MINES
    set_tight_grouping
  endif
  set_gaia_object_only

  temp_min_distance_group_placement  35
  min_distance_group_placement       6
  terrain_to_place_on                EXTRA_RESOURCE_AREA
}
